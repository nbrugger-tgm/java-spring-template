apply plugin: 'java'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'

println "Configure ${project.name}"

dependencies {
    compileOnly 'org.junit.jupiter:junit-jupiter-api:5.7.2'

    //https://docs.gradle.org/current/userguide/platforms.html#sub:bom_import
    annotationProcessor platform("org.springframework.boot:spring-boot-dependencies:${project.ext.springBootVersion}")
    implementation platform("org.springframework.boot:spring-boot-dependencies:${project.ext.springBootVersion}")
    compileOnly platform("org.springframework.boot:spring-boot-dependencies:${project.ext.springBootVersion}")
}

project.ext.loadScript = (name) -> apply from: "$rootDir/modules/${name}.gradle"

loadScript('jacoco')

java {
    sourceCompatibility = project.ext.javaVersion
    targetCompatibility = project.ext.javaVersion
    withJavadocJar()
    withSourcesJar()
}
repositories {
    mavenCentral()
}

sonarqube {
    properties {
        property 'sonar.coverage.jacoco.xmlReportPaths', "$projectDir/build/reports/jacoco/coverage.xml"
    }
}

tasks.withType(Javadoc) {
    failOnError = false
}
//allow submodule nesting
subprojects {
    apply from: "${rootDir}/modules/submodule.gradle"
}